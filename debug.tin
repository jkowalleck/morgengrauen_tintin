#NOP debug ;

#NOP :levels: ;

#VARIABLE {_dbg_lvl_none}   0;
#VARIABLE {_dbg_lvl_msg}    1; #NOP must be the smallest level;
#VARIABLE {_dbg_lvl_var}    2;
#VARIABLE {_dbg_lvl_func}   3;
#VARIABLE {_dbg_lvl_event}  4;
#VARIABLE {_dbg_lvl_gmcp}   5;
#VARIABLE {_dbg_lvl_telnet} 6;
#VARIABLE {_dbg_lvl_all}  255;


#VARIABLE {DEBUG} ${_dbg_lvl_none};




#FUNCTION {DEBUG_msg}
{
	#IF {$DEBUG >= ${_dbg_lvl_msg}}
	{
		#SHOWME {<199><FFC><aaa> :DEBUG: <799> %0 };
	}
	#RETURN {#NOP};
};




#EVENT {VARIABLE UPDATE DEBUG}
{
	@DEBUG_msg{debug level set to %1};
	@DEBUG_config_telnet{%1};
};

#FUNCTION {DEBUG_config_telnet}
{
	#IF {%1 >= ${_dbg_lvl_telnet}}
	{
		#CONFIG {debug telnet} {ON};
	}
	{
		#CONFIG {debug telnet} {OFF};
	};
	#RETURN {#NOP};
};
@DEBUG_config_telnet{$DEBUG};



#FUNCTION {DEBUG_gmcp_start}
{
	#VARIABLE {_dbg_gmcps_name} {%1};

	#IF {$DEBUG >= ${_dbg_lvl_gmcp}}
	{
		@DEBUG_msg{gmcp "${_dbg_gmcps_name}" started};
	};

	#UNVARIABLE {_dbg_gmcps_name};
	#RETURN {#NOP};
};

#FUNCTION {DEBUG_gmcp_end}
{
	#VARIABLE {_dbg_gmcpe_name} {%1};

	#IF {$DEBUG >= ${_dbg_lvl_gmcp}}
	{
		@DEBUG_msg{gmcp "${_dbg_gmcpe_name}" ended};
	};

	#UNVARIABLE {_dbg_gmcpe_name};
	#RETURN {#NOP};
};



#FUNCTION {DEBUG_event_start}
{
	#VARIABLE {_dbg_es_name} {%1};

	#IF {$DEBUG >= ${_dbg_lvl_event}}
	{
		@DEBUG_msg{event "${_dbg_es_name}" started};
	};

	#UNVARIABLE {_dbg_es_name};
	#RETURN {#NOP};
};

#FUNCTION {DEBUG_event_end}
{
	#VARIABLE {_dbg_es_name} {%1};

	#IF {$DEBUG >= ${_dbg_lvl_event}}
	{
		@DEBUG_msg{event "${_dbg_es_name}" ended};
	};

	#UNVARIABLE {_dbg_es_name};
	#RETURN {#NOP};
};


#FUNCTION {DEBUG_var}
{
	#VARIABLE {_dbg_sv_name} {%1};

	#IF {$DEBUG >= ${_dbg_lvl_var}}
	{
		@DEBUG_msg{var ${_dbg_sv_name} = ${${_dbg_sv_name}}};
	};

	#UNVARIABLE {_dbg_sv_name};
	#RETURN {#NOP};
};


#FUNCTION {DEBUG_func_start}
{
	#VARIABLE {_dbg_fs_name} {%1};
	#VARIABLE {_dbg_fs_args} {%2};

	#IF {$DEBUG >= ${_dbg_lvl_func}}
	{
		@DEBUG_msg{function "${_dbg_fs_name}" started};
	};

	@DEBUG_var{_dbg_fs_args};

	#UNVARIABLE {_dbg_fs_name};
	#UNVARIABLE {_dbg_fs_args};
	#RETURN {#NOP};
};

#FUNCTION {DEBUG_func_end}
{
	#VARIABLE {_dbg_fe_name} {%1};

	#IF {$DEBUG >= ${_dbg_lvl_func}}
	{
		@DEBUG_msg{function "${_dbg_fe_name}" ended};
	};

	#UNVARIABLE {_dbg_fe_name};
	#RETURN {#NOP};
};









#FUNCTION {_DEBUG_test}
{
	@DEBUG_func_start{_DEBUG_test;{%0}};

	@DEBUG_msg{test message};

	#VAR {_dbg_test_var} {foo};
	@DEBUG_var{_dbg_test_var};

	@DEBUG_func_end{_DEBUG_test};

	#RETURN {#NOP};
};
